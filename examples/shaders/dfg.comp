#version 450
#extension GL_ARB_separate_shader_objects : enable
#extension GL_ARB_shading_language_420pack : enable

layout(set = 0, binding = 0, std140) uniform _Size
{
	float dfg_lut_size;
};
layout(set = 0, binding = 1) uniform samplerBuffer samples;
layout(set = 0, binding = 2, rgba32f) writeonly uniform image2D output_texture;

// See Real Shading in Unreal Engine 4 (Brian Karis) for formula

float G1_Schlick(float XdotN, float k)
{
	return XdotN / (XdotN * (1.f - k) + k);
}

float G_Smith(float NdotV, float NdotL, float roughness)
{
	float k = roughness * roughness / 2.f;
	return G1_Schlick(NdotV, k) * G1_Schlick(NdotL, k);
}

// Given a Seed from a uniform distribution, returns a half vector direction
// as a theta, phi pair weighted against GGX distribution.
vec2 ImportanceSamplingGGX(vec2 Seeds, float roughness)
{
	float a = roughness * roughness;
	float CosTheta = sqrt((1.f - Seeds.y) / (1.f + (a * a - 1.f) * Seeds.y));
	return vec2(acos(CosTheta), 2.f * 3.14f * Seeds.x);
}

vec2 getSpecularDFG(float roughness, float NdotV)
{
	// We assume an implicit referential where N points in Oz
	vec3 V = vec3(sqrt(1.f - NdotV * NdotV), 0.f, NdotV);

	float DFG1 = 0., DFG2 = 0.;
	for (int i = 0; i < 1024; i++)
	{
		vec2 ThetaPhi = ImportanceSamplingGGX(texelFetch(samples, i).xy, roughness);
		float Theta = ThetaPhi.x;
		float Phi = ThetaPhi.y;
		vec3 H = vec3(sin(Theta) * cos(Phi), sin(Theta) * sin(Phi), cos(Theta));
		vec3 L = 2 * dot(H, V) * H - V;

		float NdotL = clamp(L.z, 0.f, 1.f);
		float NdotH = clamp(H.z, 0.f, 1.f);
		float VdotH = clamp(dot(V, H), 0.f, 1.f);

		if (NdotL > 0.)
		{
			float Fc = pow(1.f - VdotH, 5.f);
			float G = G_Smith(NdotV, NdotL, roughness) * VdotH / (NdotH * NdotV);
			DFG1 += (1.f - Fc) * G;
			DFG2 += Fc * G;
		}
	}
	return vec2(DFG1 / 1024., DFG2 / 1024.);
}

// Given a Seed from a uniform distribution, returns a vector direction
// as a theta, phi pair weighted against cos (n dot V) distribution.
vec2 ImportanceSamplingCos(vec2 Seeds)
{
	return vec2(acos(Seeds.x), 2.f * 3.14f * Seeds.y);
}

float getDiffuseDFG(float roughness, float NdotV)
{
	// We assume a local referential where N points in Y direction
	vec3 V = vec3(sqrt(1.f - NdotV * NdotV), NdotV, 0.f);
	float DFG = 0.f;
	for (int i = 0; i < 1024; i++)
	{
		vec2 ThetaPhi = ImportanceSamplingCos(texelFetch(samples, i).xy);
		float Theta = ThetaPhi.x;
		float Phi = ThetaPhi.y;
		vec3 L = vec3(sin(Theta) * cos(Phi), cos(Theta), sin(Theta) * sin(Phi));
		float NdotL = L.y;
		if (NdotL > 0.f)
		{
			vec3 H = normalize(L + V);
			float LdotH = dot(L, H);
			float f90 = .5f + 2.f * LdotH * LdotH * roughness * roughness;
			DFG += (1.f + (f90 - 1.f) * (1.f - pow(NdotL, 5.f))) * (1.f + (f90 - 1.f) * (1.f - pow(NdotV, 5.f)));
		}
	}
	return DFG / 1024.f;
}

layout(local_size_x = 8, local_size_y = 8, local_size_z = 1) in;

void main(void)
{
	uvec2 id = gl_GlobalInvocationID.xy;
	float roughness = .1f + .9f * float(id.x) / float(dfg_lut_size - 1);
	float NdotV = float(1 + id.y) / float(dfg_lut_size);

	vec2 specular_dfg = getSpecularDFG(roughness, NdotV);
	float diffuse_dfg = getDiffuseDFG(roughness, NdotV);
	imageStore(output_texture, ivec2(id), vec4(specular_dfg, diffuse_dfg, 1.));
}